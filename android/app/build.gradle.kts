val ciVersionCode = System.getenv("WFAS_VERSION_CODE")?.toIntOrNull()
val ciVersionName = System.getenv("WFAS_VERSION_NAME")?.takeIf { it.isNotBlank() }
val releaseKeystore = System.getenv("AUDIOBRIDGE_SIGNING_KEYSTORE")?.takeIf { it.isNotBlank() }
val releaseStorePassword = System.getenv("AUDIOBRIDGE_SIGNING_STORE_PASSWORD")?.takeIf { it.isNotBlank() }
val releaseKeyAlias = System.getenv("AUDIOBRIDGE_SIGNING_KEY_ALIAS")?.takeIf { it.isNotBlank() }
val releaseKeyPassword = System.getenv("AUDIOBRIDGE_SIGNING_KEY_PASSWORD")?.takeIf { it.isNotBlank() }

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
}

android {
    namespace = "com.cuscus.wifiaudiostreaming"
    compileSdk = 36

    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }

    signingConfigs {
        create("audiobridgeRelease") {
            if (releaseKeystore != null) {
                storeFile = file(releaseKeystore)
                storePassword = releaseStorePassword
                keyAlias = releaseKeyAlias
                keyPassword = releaseKeyPassword
            }
        }
    }

    defaultConfig {
        applicationId = "io.github.mu23xr.audiobridge"
        minSdk = 24
        targetSdk = 36
        // CI builds use a monotonically increasing versionCode so each signed
        // Chinese build can upgrade the previous one in-place. Local/upstream
        // builds keep the original version metadata.
        versionCode = ciVersionCode ?: 11
        versionName = ciVersionName ?: "1.2.1"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            signingConfig = signingConfigs.getByName("audiobridgeRelease")
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    buildFeatures {
        compose = true
        aidl = true
        buildConfig = true
    }
    packaging {
        resources {
            excludes += "META-INF/versions/**"
        }
    }
}

dependencies {

    // Lab-only privileged audio path. UserService runs our code as Android shell (uid 2000).
    implementation("dev.rikka.shizuku:api:13.1.5")
    implementation("dev.rikka.shizuku:provider:13.1.5")

    implementation(libs.androidx.profileinstaller)
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.security.crypto)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.activity.compose)
    implementation(libs.androidx.activity.ktx)
    implementation("androidx.appcompat:appcompat:1.7.0")
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.ui)
    implementation(libs.androidx.ui.graphics)
    implementation(libs.androidx.ui.tooling.preview)
    implementation(libs.androidx.material3)
    // implementation(libs.androidx.material3.jvmstubs)
    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
    androidTestImplementation(platform(libs.androidx.compose.bom))
    androidTestImplementation(libs.androidx.ui.test.junit4)
    debugImplementation(libs.androidx.ui.tooling)
    debugImplementation(libs.androidx.ui.test.manifest)
    implementation("androidx.compose.material:material-icons-extended:1.5.0")
    implementation("androidx.datastore:datastore-preferences:1.0.0")
    implementation("androidx.glance:glance-appwidget:1.1.0")
    implementation("androidx.glance:glance-material3:1.1.0")
    // ViewModel per Compose
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.8.1")

    // Ktor per il networking
    implementation("io.ktor:ktor-client-core:2.3.11")
    implementation("io.ktor:ktor-client-cio:2.3.11") // Motore client
    implementation("io.ktor:ktor-server-core:2.3.11")
    implementation("io.ktor:ktor-server-cio:2.3.11") // Motore server
    implementation("io.ktor:ktor-network:2.3.11")

    implementation("org.bouncycastle:bcprov-jdk18on:1.78.1")

    // Coroutine
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.0")

    implementation("androidx.core:core-splashscreen:1.0.1")

    implementation("androidx.graphics:graphics-shapes:1.0.1")

    // ZXing Core (Java): sola generazione dei QR di pairing.
    implementation(libs.zxing.core)
    implementation(libs.androidx.camera.core)
    implementation(libs.androidx.camera.camera2)
    implementation(libs.androidx.camera.lifecycle)
    implementation(libs.androidx.camera.view)
    implementation(libs.zxingcpp.android)
}